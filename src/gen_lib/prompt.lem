open import Pervasives_extra
open import Sail_impl_base
open import Sail_values

     import Interp_interface
(* this needs to go away: it's only so we can make the ppcmem outcome types the
same *)

val return : forall 's 'a. 'a -> outcome 's 'a
let return a = Done a

val bind : forall 's 'a 'b. outcome 's 'a -> ('a -> outcome 's 'b) -> outcome 's 'b
let rec bind m f = match m with
  | Done a ->              f a
  | Read_mem descr k ->    Read_mem descr   (fun v -> let (o,opt) = k v in (bind o f,opt))
  | Read_reg descr k ->    Read_reg descr   (fun v -> let (o,opt) = k v in (bind o f,opt))
  | Write_memv descr k ->  Write_memv descr (fun v -> let (o,opt) = k v in (bind o f,opt))
  | Write_ea descr o_s ->  Write_ea descr   (let (o,opt) = o_s in (bind o f,opt))
  | Barrier descr o_s ->   Barrier descr    (let (o,opt) = o_s in (bind o f,opt))
  | Footprint o_s ->       Footprint        (let (o,opt) = o_s in (bind o f,opt))
  | Write_reg descr o_s -> Write_reg descr  (let (o,opt) = o_s in (bind o f,opt))
  | Escape descr mo  ->    Escape descr mo
  | Fail descr ->          Fail descr
  | Error descr ->         Error descr
  | Internal descr o_s ->  Internal descr   (let (o,opt) = o_s in (bind o f ,opt))
end


type M 'a = Sail_impl_base.outcome Interp_interface.instruction_state 'a

let inline (>>=) = bind
val (>>) : forall 'b. M unit -> M 'b -> M 'b
let inline (>>) m n = m >>= fun _ -> n

val exit : forall 'a. string -> M 'a
let exit s = Fail (Just s)

val read_mem : end_flag -> bool -> read_kind -> vector bitU -> integer -> M (vector bitU)
let read_mem endian dir rk addr sz =
  let addr = address_lifted_of_bitv addr in
  let sz = natFromInteger sz in
  let k memory_value =
    let bitv = intern_mem_value endian dir memory_value in
    (Done bitv,Nothing) in
  Read_mem (rk,addr,sz) k

val write_mem_ea : write_kind -> vector bitU -> integer -> M unit
let write_mem_ea wk addr sz =
  let addr = address_lifted_of_bitv addr in
  let sz = natFromInteger sz in
  Write_ea (wk,addr,sz) (Done (),Nothing)

val write_mem_val : end_flag -> vector bitU -> M bool
let write_mem_val endian v =
  let v = extern_mem_value endian v in
  let k successful = (return successful,Nothing) in
  Write_memv v k

val read_reg_aux : reg_name -> M (vector bitU)
let read_reg_aux reg = 
  let k reg_value =
    let v = intern_reg_value reg_value in
    (Done v,Nothing) in
  Read_reg reg k

let read_reg reg =
  read_reg_aux (extern_reg_whole reg)
let read_reg_range reg i j =
  read_reg_aux (extern_reg_slice reg (i,j))
let read_reg_bit reg i =
  read_reg_aux (extern_reg_slice reg (i,i)) >>= fun v ->
  return (extract_only_bit v)
let read_reg_field reg regfield =
  read_reg_aux (extern_reg_field_whole reg regfield)
let read_reg_bitfield reg regfield =
  read_reg_aux (extern_reg_field_whole reg regfield) >>= fun v ->
  return (extract_only_bit v)

val write_reg_aux : reg_name -> vector bitU -> M unit
let write_reg_aux reg_name v =
  let regval = extern_reg_value reg_name v in
  Write_reg (reg_name,regval) (Done (), Nothing)

let write_reg reg v =
  write_reg_aux (extern_reg_whole reg) v
let write_reg_range reg i j v =
  write_reg_aux (extern_reg_slice reg (i,j)) v
let write_reg_bit reg i bit =
  write_reg_aux (extern_reg_slice reg (i,i)) (Vector [bit] i (is_inc_of_reg reg))
let write_reg_field reg regfield v =
  write_reg_aux (extern_reg_field_whole reg regfield) v
let write_reg_bitfield reg regfield bit =
  write_reg_aux (extern_reg_field_whole reg regfield)
                (Vector [bit] 0 (is_inc_of_reg reg))


val barrier : barrier_kind -> M unit
let barrier bk = Barrier bk (Done (), Nothing)


val footprint : M unit
let footprint = Footprint (Done (),Nothing)


val foreachM_inc : forall 'vars. (integer * integer * integer) -> 'vars ->
                  (integer -> 'vars -> M 'vars) -> M 'vars
let rec foreachM_inc (i,stop,by) vars body =
  if i <= stop
  then
    body i vars >>= fun vars ->
    foreachM_inc (i + by,stop,by) vars body
  else return vars


val foreachM_dec : forall 'vars. (integer * integer * integer) -> 'vars ->
                  (integer -> 'vars -> M 'vars) -> M 'vars
let rec foreachM_dec (i,stop,by) vars body =
  if i >= stop
  then
    body i vars >>= fun vars ->
    foreachM_dec (i - by,stop,by) vars body
  else return vars

let write_two_regs r1 r2 vec =
  let is_inc =
    let is_inc_r1 = is_inc_of_reg r1 in
    let is_inc_r2 = is_inc_of_reg r2 in
    let () = ensure (is_inc_r1 = is_inc_r2)
                    "write_two_regs called with vectors of different direction" in
    is_inc_r1 in

  let (size_r1 : integer) = size_of_reg r1 in
  let (start_vec : integer) = get_start vec in
  let size_vec = length vec in
  let r1_v =
    if is_inc
    then slice vec start_vec (size_r1 - start_vec - 1)
    else slice vec start_vec (start_vec - size_r1 - 1) in
  let r2_v =
    if is_inc
    then slice vec (size_r1 - start_vec) (size_vec - start_vec)
    else slice vec (start_vec - size_r1) (start_vec - size_vec) in
  write_reg r1 r1_v >> write_reg r2 r2_v
